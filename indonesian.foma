define Vowel [ a | e | i | o | u ] ;

define Consonant [
b | c | d | f | g | h | j | k | l | m | n | p | q | r | s | t | v | w | x | y | z ] ;

define Alphabet Vowel* Consonant* ;

define AssimDelRules [
p -> m || %^N "<"* _ ,, %^N -> 0 || _ "<"* p ]
.o.
%^N -> m || _ "<"* [ b | f ]
.o.
[ t -> n || %^N "<"* _ ,, %^N -> 0 || _ "<"* t ]
.o.
%^N -> n || _ "<"* [ d | c | j | s y | z ]
.o.
[ s -> n y || %^N "<"* _ ,, %^N -> 0 || _ "<"* s ]
.o.
%^N -> n g || _ "<"* [ a | e | i | o | u | g | h | k h ]
.o.
[ k -> n g || %^N "<"* _ ,, %^N -> 0 || _ "<"* k ]
.o.
%^N -> 0 || _ "<"* [ m | n | l | r | w | y ]
.o.
r -> 0 || [ b | p | t ] e _ r
.o.
r -> 0 || [ b | p ] e _ Consonant e r Consonant;

read lexc < indonesian.lexc

eliminate flag PREF

define initialnet

define intermediatenet initialnet .o. AssimDelRules ;

define InsertHyphen [..] -> %- || %> _ %< ;

define PreEq intermediatenet .o. %^R -> [Alphabet]* .o. InsertHyphen ;

define RemoveBrackets "<"|">" -> 0 ;

read regex _eq(PreEq,"<",">") .o. RemoveBrackets ;

save stack indonesian.fst
